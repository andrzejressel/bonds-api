set windows-shell := ["pwsh.exe", "-c"]

# renovate: datasource=crate depName=cargo-llvm-cov packageName=cargo-llvm-cov
CARGO_LLVM_COV_VERSION := "0.6.13"

install-requirements:
    rustup component add rustfmt
    rustup component add llvm-tools-preview
    cargo binstall --no-confirm --force cargo-llvm-cov@{{CARGO_LLVM_COV_VERSION}}

watch:
    watchexec -r -- cargo loco start

watch-prod:
    watchexec -r -- cargo loco start -e "production"

watch-test:
    watchexec -r -- cargo test

gen-api:
    npx --yes @openapitools/openapi-generator-cli@2.21.4 generate \
        -g rust-axum \
        -i .\openapi.yaml \
        --enable-post-process-file \
        -o crates/api \
        -t openapi_generator/templates/loco-rs \
        --additional-properties generateAliasAsModel=true,allowBlockingValidator=true,allowBlockingResponseSerialize=true,hideGenerationTimestamp=true

lgtm:
    docker run -p 3000:3000 -p 4317:4317 -p 4318:4318 --rm -ti grafana/otel-lgtm:0.11.6

check:
    cargo fmt --all -- --check
    cargo clippy --all-targets --all-features -- -D warnings -A clippy::uninlined_format_args

fix:
    cargo fmt --all
    cargo clippy --all-targets --all-features --fix --allow-dirty --allow-staged

test:
    cargo test --all-features --workspace

test-coverage:
    cargo llvm-cov test --all-features --workspace --cobertura --output-path cobertura.xml
    
accept-snapshots $INSTA_UPDATE="always":
    cargo test --all-features --workspace
